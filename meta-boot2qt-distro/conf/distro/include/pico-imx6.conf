############################################################################
##
## Copyright (C) 2018 The Qt Company Ltd.
## Contact: https://www.qt.io/licensing/
##
## This file is part of the Boot to Qt meta layer.
##
## $QT_BEGIN_LICENSE:GPL$
## Commercial License Usage
## Licensees holding valid commercial Qt licenses may use this file in
## accordance with the commercial license agreement provided with the
## Software or, alternatively, in accordance with the terms contained in
## a written agreement between you and The Qt Company. For licensing terms
## and conditions see https://www.qt.io/terms-conditions. For further
## information use the contact form at https://www.qt.io/contact-us.
##
## GNU General Public License Usage
## Alternatively, this file may be used under the terms of the GNU
## General Public License version 3 or (at your option) any later version
## approved by the KDE Free Qt Foundation. The licenses are as published by
## the Free Software Foundation and appearing in the file LICENSE.GPL3
## included in the packaging of this file. Please review the following
## information to ensure the GNU General Public License requirements will
## be met: https://www.gnu.org/licenses/gpl-3.0.html.
##
## $QT_END_LICENSE$
##
############################################################################

include conf/distro/include/fsl.inc

DEPLOY_CONF_NAME = "Technexion Pico iMX6"

MACHINEOVERRIDES =. "mx6:pico-imx6:"

#require conf/machine/include/imx-base.inc
#include conf/machine/include/tune-cortexa9.inc

BBMASK += "\
    meta-edm-bsp-release/recipes-extended/connman/ \
    meta-edm-bsp-release/recipes-connectivity/connman/ \
    meta-edm-bsp-release/recipes-core/images/ \
    meta-fsl-bsp-release/imx/meta-bsp/recipes-browser/chromium/ \
    meta-fsl-bsp-release/imx/meta-sdk/recipes-fsl/ \
    meta-fsl-bsp-release/imx/meta-sdk/recipes-graphics/devil/ \
    meta-fsl-bsp-release/imx/meta-bsp/recipes-security/optee-imx/optee-os-imx_git.bb \
    meta-fsl-bsp-release/imx/meta-sdk/dynamic-layers/ \
    meta-freescale/dynamic-layers/ \
"

MACHINE_SOCARCH_FILTER_append_mx6 = "virtual/libopenvg virtual/libgles1 virtual/libgles2 virtual/egl virtual/mesa virtual/libgl virtual/libg2d"

#
# Kernel Specifics
#
PREFERRED_PROVIDER_kernel ?= "linux-tn-imx"
PREFERRED_PROVIDER_virtual/kernel ?= "linux-tn-imx"

# Add kernel modules
MACHINE_EXTRA_RRECOMMENDS += "\
    kernel-module-qcacld-tn \
"

KERNEL_DEVICETREE ?= "imx6dl-pico-qca_pi.dtb imx6q-pico-qca_pi.dtb \
                      imx6dl-pico-qca_nymph.dtb imx6q-pico-qca_nymph.dtb \
                      imx6dl-pico-qca_dwarf.dtb imx6q-pico-qca_dwarf.dtb \
                      imx6dl-pico-qca_hobbit.dtb imx6q-pico-qca_hobbit.dtb \
                      imx6dl-pico_pi.dtb imx6q-pico_pi.dtb \
                      imx6dl-pico_nymph.dtb imx6q-pico_nymph.dtb \
                      imx6dl-pico_dwarf.dtb imx6q-pico_dwarf.dtb \
                      imx6dl-pico_hobbit.dtb imx6q-pico_hobbit.dtb \
"

KERNEL_IMAGETYPE = "zImage"

#
# Bootloader Specifics
#
PREFERRED_PROVIDER_u-boot = "u-boot-edm"
PREFERRED_PROVIDER_u-boot_mx6 = "u-boot-edm"
PREFERRED_PROVIDER_virtual/bootloader = "u-boot-edm"
PREFERRED_PROVIDER_virtual/bootloader_mx6 = "u-boot-edm"

UBOOT_MAKE_TARGET = ""
UBOOT_SUFFIX = "img"
SPL_BINARY = "SPL"
UBOOT_MACHINE = "pico-imx6_spl_defconfig"

# Ensure uEnv.txt will be available at rootfs time
do_rootfs[depends] += "u-boot-uenv:do_deploy"

UENV_FILENAME = "uEnv.txt"
BOOT_SCRIPTS = "${UENV_FILENAME}:uEnv.txt"

#
# Extra Stuff
#
MACHINE_FEATURES += "bluetooth pci wifi touchscreen"

MACHINE_EXTRA_RRECOMMENDS += " \
  broadcom-bluetooth \
  openssh-sftp-server \
  libsocketcan \
  bash hostapd dnsmasq haveged iptables \
"

OVERRIDES .= ":${@'' if d.getVar('RF_FIRMWARES', True) is None else ':'.join([wifi.lower() for wifi in d.getVar('RF_FIRMWARES', True).split(' ') if wifi])}"
MACHINE_FIRMWARE_remove = "linux-firmware-ath10k firmware-imx-brcm"
MACHINE_FIRMWARE_append_qca = " linux-firmware-qca-tn"
MACHINE_FIRMWARE_append_brcm = " linux-firmware-brcm-tn"
MACHINE_FIRMWARE_append_ath-pci = " linux-firmware-ath10k-tn"

#
# Peripherals
#
# Set Serial console
SERIAL_CONSOLE = "115200;ttymxc0"

# using the override image_types_fsl_tn to restrict building to sdcard.bz2 ext4 and tar.xz
IMAGE_FSTYPES += "tar.xz ext4 sdcard.bz2"
IMAGE_FSTYPES_remove = "wic wic.bmap"
IMAGE_CLASSES += "image_types_fsl_tn"
IMAGE_CLASSES_remove = "image_types_fsl"

#
# Boot2Qt specifics
#
QBSP_IMAGE_CONTENT = "\
	${IMAGE_LINK_NAME}.tar.xz \
	${IMAGE_LINK_NAME}.conf \
"

IMAGE_BOOT_FILES += " \
    u-boot-${MACHINE}.img;${MACHINEDIR}/u-boot.img \
    SPL-${MACHINE}-spl;${MACHINEDIR}/SPL \
"

# remove vulkan from distro
DISTRO_FEATURES_remove = "vulkan"

PREFERRED_VERSION_linux-tn-imx = "4.14.78"
PREFERRED_VERSION_u-boot-edm = "2018.03"
PREFERRED_VERSION_firmware-imx = "8.0"
PREFERRED_VERSION_imx-vpuwrap = "4.4.4"
PREFERRED_VERSION_imx-gpu-viv = "6.2.4.p2.3-aarch32"
PREFERRED_VERSION_imx-gpu-g2d = "6.2.4.p2.3-aarch32"
PREFERRED_VERSION_kernel-module-imx-gpu-viv = "6.2.4.p2.3"

